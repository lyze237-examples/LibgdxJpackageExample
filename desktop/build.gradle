buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.beryx:badass-runtime-plugin:1.8.5"
	}
}

apply plugin: 'application'
apply plugin: 'org.beryx.runtime'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'dev.lyze.gdxJpackage.desktop.DesktopLauncher'
eclipse.project.name = appName + '-desktop'
sourceCompatibility = 8.0

import org.gradle.internal.os.OperatingSystem

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

jar {
	destinationDirectory = file("$buildDir/lib")
	archiveFileName = "${appName}-${archiveVersion.get()}.jar"
	from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } } 
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
}

run {
	ignoreExitValue = true
}

jpackageImage.dependsOn jar

runtime {
	options = ['--strip-debug',
			   '--compress', '2',
			   '--no-header-files',
			   '--no-man-pages',
			   '--strip-native-commands',
			   '--vm', 'server']
	modules = ['java.base' ,
			   'java.desktop',
			   'jdk.unsupported']

	jpackage {
		installerOptions += [ '--vendor', 'NAME', '--description', 'SHORT DESCRIPTION' ]
		mainJar = jar.archiveFileName.get()
		installerName = imageName = appName

		if (OperatingSystem.current().windows) {
			imageOptions += [ '--win-console' ]
			//imageOptions += [ "--icon", file("../assets/icons/Icon.ico") ]
			installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut', '--win-upgrade-uuid', '47f2f058-53d7-46d4-a538-6938f2a87897']
		} else {
			//imageOptions += ["--icon", file("../assets/icons/Icon-32.png")]
			installerOptions += ['--linux-menu-group', '--linux-shortcut']
		}
	}
}
